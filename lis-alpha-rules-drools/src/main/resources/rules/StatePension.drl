package uk.nhs.nhsbsa.lis.rules.v1.droolsengine.assessment;
 

global org.slf4j.Logger logger
global uk.nhs.nhsbsa.lis.rules.v1.droolsengine.service.IAssessmentFactoryService factory
global uk.nhs.nhsbsa.lis.rules.v1.droolsengine.service.IRequireService requireService

rule "Default State Pension"
    when 
    	$index : ObjectIndex ()
        $person : Person ( type in ( PersonType.MAIN_APPLICANT, PersonType.PARTNER ) )
        exists $pensioner : IsPensioner ( person == $person )
        not Benefit( owner == $person, type == BenefitType.RETIREMENT_PENSION )
    then
        logger.info("Default State Pension");
        Benefit $benefit = factory.defaultBenefit(BenefitType.RETIREMENT_PENSION);
        $person.addBenefit($benefit);
        $index.reindex($person);
        insert ($benefit);
end

rule "Enable State Pension"
    when 
    	$assessment : Assessment ()
    	$index : ObjectIndex ()
        $person : Person ( type in ( PersonType.MAIN_APPLICANT, PersonType.PARTNER ) )
        $benefit : Benefit( owner == $person, type == BenefitType.RETIREMENT_PENSION )
        $isPensioner : IsPensioner ( person == $person )
    then
        logger.info("Enable State Pension");
        requireService.enable($assessment, $index, $benefit);
end

rule "Disable State Pension"
    when 
    	$assessment : Assessment ()
    	$index : ObjectIndex ()
        $person : Person ( type in ( PersonType.MAIN_APPLICANT, PersonType.PARTNER ) )
        $benefit : Benefit( owner == $person, type == BenefitType.RETIREMENT_PENSION )
        not IsPensioner ( person == $person )
    then
        logger.info("Disable State Pension");
        requireService.disable($assessment, $index, $benefit);
end

rule "State Pension Resources"
no-loop true
when 
	not IncomeCapital( type == BenefitType.RETIREMENT_PENSION.name() )
    $breakdown : Breakdown()
	$person : Person()
	accumulate ( Benefit ( 
			type == BenefitType.RETIREMENT_PENSION,
			receiving == true, value.canConvert(),
			owner == $person,
			$value : value.convert(Interval.WEEKLY).value );
		$sum : sum ( $value )
	)
then
	IntervalValue total = new IntervalValue(Interval.WEEKLY, $sum);
	IncomeCapital requirement = new IncomeCapital(BenefitType.RETIREMENT_PENSION.name(), 
		$person.toString(), 
		total.getValue(),
		null, null, null);
	$breakdown.addIncomeCapital(requirement);
	insert ( requirement );
end


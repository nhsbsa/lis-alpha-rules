package uk.nhs.nhsbsa.lis.rules.v1.droolsengine.assessment;
 
import uk.nhs.nhsbsa.lis.rules.v1.model.*;

global org.slf4j.Logger logger
global uk.nhs.nhsbsa.lis.rules.v1.droolsengine.service.IAssessmentFactoryService factory

rule "Default Application"
    when 
        $assessment : Assessment( )
        not LisApplication( )
    then
        logger.info("Default Application");
        LisApplication $application = factory.defaultApplication();
        modify ($assessment) { setApplication($application) };
        insert ($application);
end

rule "Default Applicant"
    when
        $application: LisApplication ( applicant == null )
    then
        logger.info("Default Applicant");
        Person $applicant = factory.defaultPerson(PersonType.MAIN_APPLICANT);
        modify ($application) { setApplicant($applicant) };
        insert ($applicant);
end

rule "Default Partner"
    when
        $application: LisApplication ( partner == null )
    then
        logger.info("Default Partner");
        Person $partner = factory.defaultPerson(PersonType.PARTNER);
        modify ($application) { setPartner($partner) };
        insert ($partner);
end



	